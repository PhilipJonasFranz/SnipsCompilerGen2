#include<linked_list.sn>

/*
 * This package provides the utility to create and 
 * use a stack. This package builds on the linked_list
 * package. 
 */
namespace Stack {
	
	restricted struct StackedList<T> {
		
		LinkedList<T>* list;
		
		/*
		 * Destroys the stack and the capsuled list.
		 */
		shared void destroy<T:void>() {
			/* Destroy the capsuled list */
			(self->list)->destroy();
			
			/* Free the stack itself */
			free(self);
		}
		
		/*
		 * Pushes given element on top of the stack.
		 */
		shared void push<T>(T value) {
			(self->list)->add<T>(value);
		}
		
		/*
		 * Returns true if no elements are currently in the stack.
		 */
		shared bool isEmpty<T:void>() {
			return (self->list)->size() == 0;
		}
		
		/*
		 * Returns the amount of elements stored in the stack.
		 */
		shared int size<T:void>() {
			return (self->list)->size();
		}
		
		/*
		 * Returns the element on the top of the stack, but doesnt
		 * remove it. Returns the default value if the stack is empty.
		 */
		shared T peek<T>() {
			if (self->isEmpty<T>()) return self->list->defValue;
			else return (self->list)->get<T>((self->list)->size<T>() - 1);
		}
		
		/*
		 * Returns the element on the top of the stack, and 
		 * removes it. Returns the default value if the stack is empty.
		 */
		shared T pop<T>() {
			if (self->isEmpty<T>()) {
				return self->list->defValue;
			}
			else {
				int size = (self->list)->size();
				T value = (self->list)->get<T>(size - 1);
				(self->list)->remove<T>(size - 1);
				return value;
			}
		}
		
	}
	
	/*
	 * Creates and initializes a new stack.
	 * Returns a pointer to the newly created stack.
	 */
	shared StackedList<T>* create<T>(T defValue) {
		/* Allocate a new linked list for the stack */
		LinkedList<T>* list = List::create<T>(defValue);
		
		/* Initialize the stack with the list */
		return init<>(StackedList<T>::(list));
	}
	
} 
