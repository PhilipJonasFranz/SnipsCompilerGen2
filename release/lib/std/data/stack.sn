#include<linked_list.sn>
#include<iterable.sn>

namespace Stack {
	
	restricted struct StackedList<T> : Iterable<T> {
		
		shared void destroy<T:void>() {
			/* Destroy the capsuled list */
			(self->list)->destroy();
			
			/* Free the stack itself */
			free(self);
		}
		
		shared void push(T value) {
			(self->list)->add(value);
		}
		
		shared bool isEmpty<T:void>() {
			return (self->list)->size() == 0;
		}
		
		shared int size<T:void>() {
			return (self->list)->size();
		}
		
		shared T peek() {
			if (self->isEmpty()) return self->list->defValue;
			else return (self->list)->get((self->list)->size() - 1);
		}
		
		shared T pop() {
			if (self->isEmpty()) {
				return self->list->defValue;
			}
			else {
				int size = (self->list)->size();
				T value = (self->list)->get(size - 1);
				(self->list)->remove(size - 1);
				return value;
			}
		}
		
		shared T get(int index) {
			return (self->list)->get(index);
		}
		
		shared void set(int index, T value) {
			(self->list)->set(index, value);
		}
		
		static StackedList<T>* create<T>(T defValue) {
			/* Allocate a new linked list for the stack */
			LinkedList<T>* list = LinkedList::create(defValue);
			
			/* Initialize the stack with the list */
			return init<>(StackedList<T>::(list));
		}
		
	}
	
} 
